{-# LANGUAGE GeneralizedNewtypeDeriving #-}

module Briefcase.CashFlow where

import Data.Hourglass (Date(..), Period(..), dateAddPeriod)

import Core.Text
import Briefcase.Money

data CashFlow = CashFlow
    { dateOf :: Date
    , amountOf :: Money
    , nameOf :: Rope
    }
    deriving (Show)

{-|
Given an (infinite) list of CashFlows, extract the flows that are within
the specified date range. Relies on the assumption that the list of flows
is in ascending order.
-}
range :: Date -> Date -> [CashFlow] -> [CashFlow]
range from unto list = start list
  where
    start [] = []
    start (flow:flows) = if (from > dateOf flow)
        then start flows
        else finish (flow:flows)

    finish [] = []
    finish (flow:flows) = if dateOf flow > unto
        then []
        else flow : finish flows

quarterly :: Rope -> Money -> Date -> [CashFlow]
quarterly = stream quarterlyDates

monthly :: Rope -> Money -> Date -> [CashFlow]
monthly = stream monthlyDates

fortnightly :: Rope -> Money -> Date -> [CashFlow]
fortnightly = stream fortnightlyDates

stream :: (Date -> [Date]) -> Rope -> Money -> Date -> [CashFlow]
stream f name amount seed =
  let
    dates = f seed
  in
    fmap (\date -> CashFlow { dateOf = date, amountOf = amount, nameOf = name }) dates


{-|
Extract a range of dates from a list. This would be useful to say "the cash
flows for next year", thus bridging between a known time range and infinite
lists generated by the repeating functions below
-}
rangeDates :: Date -> Date -> [Date] -> [Date]
rangeDates from unto list = start list
  where
    start [] = []
    start (date:dates) = if (from > date)
        then start dates
        else finish (date:dates)

    finish [] = []
    finish (x:xs) = if x > unto
        then []
        else x : finish xs

quarterlyDates :: Date -> [Date]
quarterlyDates = increment (Period 0 3 0)

monthlyDates :: Date -> [Date]
monthlyDates = increment (Period 0 1 0)

fortnightlyDates :: Date -> [Date]
fortnightlyDates = increment (Period 0 0 14)

{-|
Return an infinite lists of dates incrementing by the period specified,
starting from (and including) the given seed date.
-}
increment :: Period -> Date -> [Date]
increment delta from =
  let
    next = dateAddPeriod from delta
  in
    from : increment delta next
